\section{staticKanren}\label{static}
This section introduces staticKanren, a language designed to analyze miniKanren programs. We first step through a few simple examples in section \ref{static_intro} to develop intuition for the detailed implementation in section \ref{static_imp}. Section \ref{S} and \ref{au} demonstrate some applications of the language.

\subsection{Introductory examples}\label{static_intro}
On the surface, staticKanren was designed to look as close to miniKanren as possible; however there are still some fundamental differences. Unlike miniKanren, staticKanren cannot deal with infinite answer streams. In fact, staticKanren programs can only return all answers using \code{run*} as there is no point withholding already-computed answers. As an unfortunate result, we cannot write non-trivial recursive relations in staticKanren as-is. But on the bright side, this also gives the language a simpler implementation.

staticKanren offers only three primitives constraints: equality, disequality and \textbf{fake}. The first two retain their syntax and semantics from miniKanren, whereas the third is a new addition to emulate recursive relations. There is no reason not to include more primitive constraints as the more knowledge staticKanren has, the better answers it can return. However, adding other constraints would be a distraction from our goal, so we only include disequality because it plays such an important role in many relations.

The core idea of staticKanren is the observation that relational programs' answers are also relational programs. And because answers are interpreted as programs, the language must treat variable names more seriously. For example (in this section, $\Rightarrow$ means "evaluating under staticKanren"):

\begin{lstlisting}
(run* (q p) (== q 4))
\end{lstlisting}
$\Rightarrow$ \code{(((4 #(p 0)) () ()))}

If the above program were run under miniKanren instead, the answer would have been \code{((4 _.0))}. The variable \code{p} remains unknown in the answer, so it gets to keep its name. Remember that terms of the form \code{#(...)} are vectors, thus the variable bound to \code{p} is the vector \code{#(p 0)}\footnote{Variables in miniKanren have always been vectors, but we cannot not see that because variables are always reified to symbols at the end.} (let us ignore the number on the right for now). Besides the query variables' values, staticKanren always returns two additional constraint stores, one for disequalities and one for fake constraints. Hence, every answer has the normal shape of a three-element list. The next example demonstrates how the language deals with name conflict:

\begin{lstlisting}
(run* (q a) (fresh (a d) (== q `(,a . ,d))))
\end{lstlisting}
$\Rightarrow$ \code{((((#(a 1) . #(d 1)) #(a 0)) () ()))}

The same program under miniKanren would return \code{(((_.0 . _.1) _.2))} instead. There are two variables having the same name \code{a}. staticKanren can still distinguish these two variables by their \textbf{birthdate} (the number shown on the right). Meanwhile, miniKanren sidesteps this problem completely by introducing a new name for every unbound variable appearing in the answer. It is perhaps a subjective matter to debate which representation is better, but there is one at least one theoretical problem avoided by the new approach\footnote{This example was even mentioned in one of the online miniKanren's Uncourse Hangouts organized by William Byrd.}:

\begin{lstlisting}
(run* (q p) (== q '_.0))
\end{lstlisting}
$\Rightarrow$ \code{(((_.0 #(p 0)) () ()))}

The same program under miniKanren would return \code{((_.0 _.0))}!. And because two Scheme symbols are the same iff they look the same, there is no way to distinguish \code{p} and \code{q} in that case, even from the computer's perspective. The last example demonstrates fake constraints. It is a meaning program to stress that these fake constraints have no actual meaning since the \code{fake} form does no more than evaluating its body and add the resulting expression to the fake constraint store.
\begin{lstlisting}
(run* (q p r)
  (fake `(mother ,q ,p)) (fake `(father ,p ,r))))
\end{lstlisting}

$\Rightarrow$
\begin{lstlisting}
(((#(q 0) #(p 0) #(r 0))
 ()
 ((father #(p 0) #(r 0))
  (mother #(q 0) #(p 0)))))}
\end{lstlisting}



\subsection{Implementation}\label{static_imp}
This section presents an R6RS compliant implementation of staticKanren. We will highlight only those parts that demonstrates interesting differences from the standard miniKanren implementation describe by \textcite{byrdphd}\footnote{The full implementation is available at \url{https://github.com/lackhoa/staticKanren}.}.

The following functions construct and dispatch variables. Variables are vectors of two elements: its name (a symbol) and its birthdate (a number). Additionally, we would like to have a total order on variables defined by \code{var<?}. A variable is greater than another if its birthdate is less (i.e. it was introduced sooner), or if it has the same birthdate and a lexicographically lesser name. We call greater variables \textbf{seniors} and lesser ones \textbf{juniors}.
\begin{lstlisting}
(;; name is a symbol, bd is a number
 define var (lambda (name bd) (vector name bd)))
(define var->name (lambda (var) (vector-ref var 0)))
(define var->bd (lambda (var) (vector-ref var 1)))
(define var? vector?)
(define var<?
  ;; v1 is prioritized over v2
  (lambda (v1 v2)
    (let ([n1 (symbol->string (var->name v1))]
          [bd1 (var->bd v1)]
          [n2 (symbol->string (var->name v2))]
          [bd2 (var->bd v2)])
      (or (< bd1 bd2)
          (and (= bd1 bd2) (string<? n1 n2))))))
\end{lstlisting}

Next we have definitions about constraints. Many of these functions could easily be defined automatically using records, but we choose to use lists to retain conceptual simplicity and flexibility. A constraint is a 4-tuple consisting of a substitution \code{S}, a date counter \code{C}, a disequality store \code{D} and a fake constraint store \code{F}. The counter starts from 0 and only goes upwards. Additionally, \code{letg@} is a special form to dispatch on states.
\begin{lstlisting}
(define-syntax letg@
  ;; let with state inspection
  (syntax-rules (:)
    [(_ (c : s* ...) e)
     (let ([s* (c-> c 's*)] ...) e)]))
(define all-constraints '(S C D F))
(define init-S '())
(define init-C 0)
(define init-D '())
(define init-F '())
(define make-c (lambda (S C D F) (list S C D F)))
(define init-c (make-c init-S init-C init-D init-F))
(define c->
  (lambda (c store)
    (rhs (assq store (map list all-constraints c)))))
(define update-S (lambda (c S) (letg@ (c : C D F) (make-c S C D F))))
(define update-C (lambda (c C) (letg@ (c : S D F) (make-c S C D F))))
(define update-D (lambda (c D) (letg@ (c : S C F) (make-c S C D F))))
(define update-F (lambda (c F) (letg@ (c : S C D) (make-c S C D F))))
\end{lstlisting}

The final piece of groundwork concerns the answer stream monad. Because there are no lazy streams, definitions are trivial to implement and we keep them here only for the sake of comparison.
\begin{lstlisting}
(define mzero (lambda () '()))
(define unit (lambda (x) `(,x)))
(define choice (lambda (x y) `(,x . ,y)))
(define mplus append)
(define bind (lambda (c* g) (apply mplus (map g c*))))
\end{lstlisting}

\code{unify} takes two terms along with a substitution and returns the extended substitution where these two terms are made the same. In the case that the two terms are always different, however, \code{unify} returns \code{#f}. There is a new clause in staticKanren to handle the case where both walked terms are variables, we make sure that seniors are always on the rhs. That way, walked seniors never result in juniors.
\begin{lstlisting}
(define unify
  (lambda (t1 t2 S)
    (let ([t1 (walk t1 S)]
          [t2 (walk t2 S)])
      (cond
       [(eq? t1 t2) S]
       [(and (var? t1) (var? t2))
        (or (and (var<? t2 t1)
                 (extend S t1 t2))
            (extend S t2 t1))]
       [(var? t1) (extend-check t1 t2 S)]
       [(var? t2) (extend-check t2 t1 S)]
       [(and (pair? t1) (pair? t2))
        (let ([S+ (unify (car t1) (car t2) S)])
          (and S+ (unify (cdr t1) (cdr t2) S+)))]
       [(equal? t1 t2) S]
       [else #f]))))
\end{lstlisting}

The new clause (to compare variables) adds a little runtime overhead, but that does not matter in this case because this function simply is not used at runtime. There is a method to achieve the same effect without modifying \code{unify} by tweaking the reification process instead, which should eliminate the runtime overhead. However, doing it in this way keeps the substitution list clean and easy to work with. This cleanliness is helpful in section \ref{au} where we have to modify the substitution list after reification.

Figures \ref{miniS} and \ref{staticS} show miniKanren's and staticKanren's representation of the same substitution list, assuming a variable-only substitution. Vertices correspond to variables. Each edge from vertex \code{x} to vertex \code{y} corresponds to an association with lhs variable \code{x} and rhs variable \code{y}.

\begin{figure}[h]
    \centering
    \begin{minipage}{.5\textwidth}
        \includegraphics[width=.5\textwidth]{figures/coverbg.png}
        \captionof{figure}{miniKanren's substitution list}
        \label{miniS}
    \end{minipage}%
    \begin{minipage}{.5\textwidth}
        \includegraphics[width=.5\textwidth]{figures/coverbg.png}
        \captionof{figure}{staticKanren's substitution list}
        \label{staticS}
    \end{minipage}
\end{figure}

Now we are ready to define user-level functions, starting with fake constraint.
\begin{lstlisting}
(define fake
  (lambda (expr)
    (lambdag@ (c : F)
      (unit (update-F c `(,expr . ,F))))))
\end{lstlisting}

Next is \code{fresh}, a conjunction in the body of a \code{letv}. \code{letv} extracts the date counter from its input state and assign it to the new variables; the counter is then incremented in the resulting states.
\begin{lstlisting}
(define-syntax fresh
  (syntax-rules ()
    [(_ (x* ...) g g* ...)
     (letv (x* ...) (conj g g* ...))]))

(define-syntax letv
  (syntax-rules ()
    [(_ (x* ...) g)
     (lambdag@ (c : C)
       (let ([x* (var 'x* C)] ...)
         (g (update-C c (+ C 1)))))]))
\end{lstlisting}

The fascinating part is that reification can be made even simpler due to the fact that unknown variables get to keep their names. For the final result, \code{reify} only need to deep walk the query variables and the constraint stores in \code{S}. A more compact representation of the disequality store is then obtained by removing constraints that containing either no variable or irrelevant variable(s) (i.e. variable that does not occur in the final version of \code{q*} or \code{F}).
\begin{lstlisting}
(define reify
  ;; This will return a c with clausal S
  (lambda (c q*)
    (letg@ (c : S D F)
      (let ([t (walk* q* S)]
            [D (walk* D S)]
            [F (walk* F S)])
        (let ([R (get-vars `(,t ,F))])
          (let ([D (rem-subsumed (purify-D D R))])
            `(,t ,D ,F)))))))
\end{lstlisting}

For closure, helpers of \code{reify} are given below. It is worth noting that we treat constraint stores as normal miniKanren terms, which explains why the code is so short. Additionally, \code{case-term} is a macro form which helps dispatch on term. There are three cases for a term: variable, pair and atom. The cases are treated in that order.
\begin{lstlisting}
(define get-vars
  (lambda (t)
    (case-term t
      [v `(,v)]
      [(a d) (append (get-vars a) (get-vars d))]
      [a '()])))

(define purify-D
  (lambda (D R)
    (filter (lambda (d)
              (not (or (constant? d)
                       (has-iv? d R))))
            D)))

(define constant?
  (lambda (t)
    (case-term t
      [v #f]
      [(a d) (and (constant? a) (constant? d))]
      [atom #t])))

(define has-iv?
  (lambda (t R)
    (let has-iv? ([t t])
      (case-term t
        [v (not (memq v R))]
        [(a d) (or (has-iv? a) (has-iv? d))]
        [atom #f]))))
\end{lstlisting}

The definition \code{run*} concludes the core implementation of staticKanren.
\begin{lstlisting}
(define-syntax run*
  (syntax-rules ()
    [(_ (q q* ...) g g* ...)
     ((fresh (q q* ...)
        g g* ...
        (finalize `(,q ,q* ...)))
      init-c)]))

(define finalize
  (lambda (qs)
    (lambdag@ (final-c)
      (unit (reify final-c qs)))))
\end{lstlisting}

\subsection{Converting answer clausal form into  substitution}\label{S}
Let us begin exploring the practical value of staticKanren with a simple problem of returning substitutions instead of walked query variables in the answers. We achieve this with the new \code{run*su} form. The answers in the next example would be \code{((#(q 0) #(q 0) #(q 0)) () ())} if we replaced \code{run*su} by \code{run*}.
\begin{lstlisting}
(run*su (q p r) (== q p) (== p r))
\end{lstlisting}
$\Rightarrow$ \code{((((#(r 0) #(p 0)) (#(q 0) #(p 0))) () ()))}

We see that the answer looks exactly like the program, which once again highlights the fact that miniKanren states are just normalized accumulations of logical assertions throughout the execution of programs, albeit normalized. By looking at the answers, we can summarize the content of the program that produced them. As a matter of fact, answers may be simpler than programs, as the following case shows.
\begin{lstlisting}
(pp (run*su (q p) (== q p) (== p q)))
\end{lstlisting}
$\Rightarrow$ \code{((((#(q 0) #(p 0))) () ()))}

Since \code{==} is commutative, the assertion \code{(== p q)} is redundant and it is removed from the final answer. It might look as if a complex inference mechanism is being used, but in fact this feature only requires a simple change to \code{run*}. Unification has already done much of the work building up a clean substitution, we only need to retrieve it back after it has been cleaned up even more by \code{reify}. Without further hand-waving, here is \code{run*su}.
\begin{lstlisting}
(define-syntax run*su
  ;; run* with substitutions
  (syntax-rules ()
    [(_ (q q* ...) g g* ...)
     (let ([q (var 'q init-C)] [q* (var 'q* init-C)] ...)
       (let ([qs `(,q ,q* ...)])
         (let ([c* ((conj g g* ... (finalize qs))
                    (update-C init-c (+ init-C 1)))])
           (map (lambda (c) (su c qs)) c*))))]))

(define su
  (lambda (c qs)
    `(,(unify (car c) qs init-S)
      .
      ,(cdr c))))
\end{lstlisting}

This much of development already lets us to obtain the "negative" side of the \code{lookupt} function defined earlier in section \ref{reif}. However, we first need to redefine \code{lookupt} so that the recursive call is fake.
\begin{lstlisting}
(define lookupt
  (lambda (x env t)
    (lambda (bound?)
      (conde
       [Unchanged ...]
       [(fresh (y b rest)
          (== `((,y . ,b) . ,rest) env)
          (condo
           [Unchanged ...]
           [else
            (;; The only place that is changed
             fake `((lookupt ,x ,rest ,t) ,bound?))]))]))))
\end{lstlisting}

\begin{lstlisting}
(run*su (x env) ((lookupt x env 'unbound) #f))
\end{lstlisting}
$\Rightarrow$
\begin{lstlisting}
(((#(x 0) ()) 
  () 
  ())
 ((#(x 0) 
   ((#(y 1) . #(b 1)) . #(rest 1)))
  (((#(y 1) #(x 0))))
  (((lookupt #(x 0) #(rest 1) unbound) #f))))
\end{lstlisting}
This response is akin to the goal:
\begin{lstlisting}
(conde
 [(== '() x)]
 [(== `((,y . ,b) . ,rest) x)
  (=/= y x)
  ((lookupt x rest 'unbound) #f)])
\end{lstlisting}
The second clause is particularly interesting: it states that if the environment is non-empty and the lookup is to fail, then the variable \code{x} must not match the association's lhs and the lookup must also fail for the rest of the environment.

\subsection{Analyzing common unification pattern with anti-unification}\label{au}
There are still cases where the answers returned do not look at all similar to the program that generated them. Take \code{membero} for example:
\begin{lstlisting}
(define membero
  (lambda (x ees)
    (fresh (e es)
      (== ees `(,e . ,es))
      (condo
       [(==t x e) succeed]
       [else (fake `(membero ,x ,es))]))))
\end{lstlisting}

Rewritten through \code{run*su}, this gives.
\begin{lstlisting}
((((#(ees 0) (#(x 0) . #(es 1)))) 
  ()
  ())
 (((#(ees 0) (#(e 1) . #(es 1))))
  (((#(e 1) #(x 0))))
  ((membero #(x 0) #(es 1)))))
\end{lstlisting}
Which is equivalent to this program.
\begin{lstlisting}
(define membero
  (lambda (x ees)
    (conde
     [(fresh (es) (== `(,x . ,es) ees))]
     [(fresh (e es)
        (== `(,e . ,es) ees)
        (=/= e x)
        (membero x es))])))
\end{lstlisting}
Human readers would agree that this version does not look at all natural: it does not show the common structure of \code{ees} in both clauses. To make this notion of commonness more clear, we refer to \textbf{anti-unification}, which as the name suggests is a dual of unification. Given a collection of terms, anti-unification returns their least general generalization. This process describes quite well humans' ability to recognize pattern over symbolic formulae, which is exactly what we need for this purpose. For example,
\begin{lstlisting}
(let ([t* '((1 * 2 = 2 + 1)
            (4 * 3 = 3 + 4))])
  (anti-unify t*))
\end{lstlisting}
$\Rightarrow$ \code{(#(au0 0.5) * #(au1 0.5) = #(au1 0.5) + #(au0 0.5))}

Anti-unification has captured the common pattern from these two formulae. \code{au0} and \code{au1} are normal staticKanren variables introduced in the process, which from now on shall be referred to as \textbf{pattern variables}. All pattern variables have the birthdate \code{0.5}, the reason for which will become clear when the program is complete.

The implementation of \code{anti-unify} is adapted from \textcite{ostvold2004functional}. The biggest difference is that this version also recognizes identical variables as being the same terms and reuse that variable in the pattern. 

\begin{lstlisting}
(define anti-unify
  (lambda (t*)
    (let-values
        ([(res _iS)
          (let au ([t* t*] [iS '()])
            (cond
             [;; rule 7: eq? deals with variables too
              (for-all (eqp? (car t*)) (cdr t*))
              (values (car t*) iS)]
             [;; rule 8
              (for-all pair? t*)
              (let-values ([(a iS+) (au (map car t*) iS)])
                (let-values ([(d iS++)
                              (au (map cdr t*) iS+)])
                  (values `(,a . ,d) iS++)))]
             [;; rule 9
              (find (lambda (s) (teq? (lhs s) t*)) iS)
              =>
              (lambda (s) (values (rhs s) iS))]
             [;; rule 10
              else
              (let ([new-var
                     (var (au-name (length iS)) AU-BD)])
                (values new-var (extend iS t* new-var)))]))])
      res)))

(define eqp? (lambda (u) (lambda (v) (eq? u v))))

(define au-name
  (lambda (n)
    (string->symbol
     (string-append "au" (number->string n)))))
\end{lstlisting}

Even though anti-unification is the heart of our solution, the road to victory is not laid out just yet. It is not clear how to convert the obtained pattern into a usable program. We start by making a small step similar to section \ref{S}.
\begin{lstlisting}
(define-syntax run*au
  ;; run* with anti-unification analysis
  (syntax-rules ()
    [(_ (q q* ...) g g* ...)
     (let ([q (var 'q init-C)] [q* (var 'q* init-C)] ...)
       (let ([qs `(,q ,q* ...)])
         (let ([c* ((conj g g* ... (finalize qs))
                    (update-C init-c (+ init-C 1)))])
           (au-extract c* qs))))]))
\end{lstlisting}

The main work is now delegated to \code{au-extract}, which takes the answers and the query variables as input. First, \code{au-extract} extracts a common pattern \code{au} from the answer term \code{t*}. Second, it unifies the queries variables with \code{au} in the empty environment, resulting in the substitution \code{uS}. The next obvious step is to unify \code{au} back into each of the answer term in \code{t*} to obtain the substitutions \code{S*}. Finally, we clean up the substitutions with \code{purify-S} and walk the other constraints in those substitutions.
\begin{lstlisting}
(define au-extract
  (lambda (c* qs)
    (let ([t* (map car c*)]
          [D* (map cadr c*)]
          [F* (map caddr c*)])
      (let ([au (anti-unify t*)])
        (let ([auS (unify qs au init-S)])
          (let ([S* (map (lambda (t)
                           (prefix-unify au t auS))
                         t*)])
            `(,(purify-S auS init-C)
              ,(map au-helper S* D* F*))))))))

(define prefix-unify
  (lambda (t1 t2 S) (prefix-S (unify t1 t2 S) S)))

(define au-helper
  (lambda (S D F)
    (let ([S (purify-S S AU-BD)]
          [D (walk* D S)]
          [F (walk* F S)])
      `(,S ,D ,F))))

(define AU-BD (+ init-C 0.5))
\end{lstlisting}

The only question left is how to clean up the substitutions. We have already done a similar thing during reification with the help of \code{walk*}, but this time things are not so convenient because we also want to retain the intermediate associations given by anti-unification. There is a basic principle that works well: an association is redundant iff its lhs (a variable) was not introduced prior to a certain date. The intuitive reason is that we can just use the rhs without ever introducing the lhs in the first place.

Conveniently, \code{purify-S} can keep track of already introduced variables using their birthdates. Associations whose lhs was born later than a certain date (called the \textbf{cutoff date}) are filtered out of the substitution list. We need only look at the lhs because it is always the junior variable in the case that both sides are variables.
\begin{lstlisting}
(define purify-S
  (lambda (S cutoff)
    (filter (lambda (s) (<= (var->bd (lhs s)) cutoff))
            S)))
\end{lstlisting}

Looking back, we see that \code{purify-S} is called twice in \code{au-extract} and \code{au-helper}. The first call is to clean up the common substitution involving query variables and pattern variables, in which case the cutoff date only allows query variables (note that \code{init-C} is also the birthdate of all query variables). In the second call (in \code{au-helper}) which involves case-specific substitutions, we also include pattern variables since they have already been introduced. This explains why pattern variables are born on \code{0.5}: they are junior to query variables, but senior to all variables introduced by the "user" (i.e. variables coming from \code{fresh}). Figure \ref{fig:au_S} illustrates the idea.
\begin{figure}[h]
    \centering
    \includegraphics{figures/logo.jpg}
    \caption{The interaction between query, pattern and "user" variables}
    \label{fig:au_S}
\end{figure}


The implementation is now complete and we can start using \code{run*au} to rewrite programs. The call below will return \code{lookupo}, obtained by forcing \code{lookupt} to "return" \code{#t}. Notice that this \code{lookupt} is the same modified version in section \ref{S}.
\begin{lstlisting}
(run*au (x env t) ((lookupt x env t) #t))
\end{lstlisting}
$\Rightarrow$
\begin{lstlisting}
(((#(env 0)
   ((#(au0 0.5) #(au1 0.5)) . #(rest 1))))
 ((((#(au1 0.5) #(t 0))
    (#(au0 0.5) #(x 0)))
   ()
   ())
  (()
   (((#(au0 0.5) #(x 0))))
   (((lookupt #(x 0) #(rest 1) #(t 0))
     #t)))))
\end{lstlisting}
This response is akin to the goal:
\begin{lstlisting}
(fresh (au0 au1)
  (== `((,au0 . ,au1) . ,rest) env)
  (conde
   [(== au0 x) (== t au1)]
   [(=/= au0 x)
    ((lookupt x rest t) #t)]))
\end{lstlisting}
Anti-unification identifies that across all clauses, the environment is always a non-empty list with the first element being an association. It also correctly handles how each clause has to do afterwards to obtain the original semantics. Especially the third clause is a clever assertion: it says that if the association's lhs (\code{au0}) is different from the variable \code{x} and the lookup is to succeed, then the lookup of \code{x} in the tail of the environment has to succeed.

To aid in understanding what the role of \code{purify-S} is in the example above, here is the same answer but with both calls to \code{purify-S} removed:
\begin{lstlisting}
(((#(env 0)
   ((#(au0 0.5) #(au1 0.5)) . #(rest 1))))
 ((((#(au1 0.5) #(t 0))
    (#(au0 0.5) #(x 0)))
   ()
   ())
  (((#(b 1) #(au1 0.5))
    (#(y 1) #(au0 0.5)))
   (((#(au0 0.5) #(x 0))))
   (((lookupt #(x 0) #(rest 1) #(t 0))
     #t)))))
\end{lstlisting}
This is akin to the goal:
\begin{lstlisting}
(fresh (au0 au1)
  (== `((,au0 ,au1) . ,rest) env)
  (conde
   [(== au0 x) (== au1 t)]
   [(== b au1) (== y au0)
    (=/= au0 x)
    ((lookupt x rest t) #t)]))
\end{lstlisting}
The two \code{==}'s involving \code{b} and \code{y} are not necessary and were removed by the second call to \code{purify-S} in the original answer due to the fact that \code{b} and \code{y} were born after \code{AU-BD} (0.5).